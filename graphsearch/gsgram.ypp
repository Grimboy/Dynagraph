%{
#pragma prototyped
#include "graphsearch/gscmds.h"
%}

%token <i> T_graph T_node T_edge T_pattern T_search T_input
%token <i> T_open T_close T_insert T_delete T_modify T_lock T_unlock T_segue T_define
%token <str> T_id
%token <i> T_edgeop T_subgraph
%type <str> viewid

%union	{
			int				i;
			char			*str;
}

%%

session		:  commands 
//			|  /* empty */
			;

commands	:	commands command '\n'
			|	/* empty */
			;

command		:
			ins_node | ins_edge
			| mod_node | mod_edge
			| del_node | del_edge
			| open_graph | close_graph | mod_graph
			| lock_graph | unlock_graph
			| segue
			| define_pattern | define_search | define_input
			| nop
			|  error	{gs_abort(IF_ERR_SYNTAX);}
			;

nop			:	/* empty */
			;

open_graph	:	T_open T_graph open_graph2 {}
			;

open_graph2 : viewid attrlist	{gs_open_graph($1);}
			;

close_graph	:	T_close T_graph viewid		{gs_close_graph($3);}
			;

mod_graph	:	T_modify T_graph viewid attrlist {gs_mod_graph($3);}
			;

lock_graph : T_lock T_graph viewid {gs_lock($3);}
			;

unlock_graph : T_unlock T_graph viewid {gs_unlock($3);}
			;

segue : T_segue T_graph viewid {gs_segue($3);}
			;

define_pattern : T_define T_pattern { gs_define_pattern(); }
			;

define_search : T_define T_search { gs_define_search(); }
			;

define_input : T_define T_input { gs_define_input(); }
			;

ins_node	:	T_insert T_node viewid T_id attrlist	{gs_ins_node($3,$4);}
			;

mod_node	:	T_modify T_node viewid T_id attrlist	{gs_mod_node($3,$4);}
			;

del_node	:	T_delete T_node viewid T_id	{gs_del_node($3,$4);}
			;

ins_edge	:	T_insert T_edge viewid T_id T_id T_id attrlist {gs_ins_edge($3,$4,$5,$6);}
			;

mod_edge	:	T_modify T_edge viewid T_id attrlist {gs_mod_edge($3,$4);}
			;

del_edge	:	T_delete T_edge viewid T_id	{gs_del_edge($3,$4);}
			;

attrlist	: 	'[' {gs_reset_attrs();} optattrdefs ']'
			|	{gs_reset_attrs();} /* empty */
			;

optattrdefs	: 	attrdefs
			| /* empty */
			;

attrdefs	:  attritem
			|  attrdefs optsep attritem
			;

attritem	:  T_id '=' T_id			{gs_append_attr($1,$3);}
			;

optsep 		:  ';' | ',' | /*empty*/ ;
 
viewid		:	T_id {$$ = $1; }	
			;
